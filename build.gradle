plugins {
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
  maven {
    name = "Jared's maven"
    url = "https://maven.blamejared.com/"
  }
  flatDir { dirs 'deps' }
}

version = "${minecraft_version}-${mod_version}"
group= 'com.girafi.impstorage'

base {
    archivesName = "ImpracticalStorage"
}

minecraft {
    mappings channel: 'official', version: project.minecraft_version
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
	enableIdeaPrepareRuns = true
	copyIdeResources = true
	generateRunFolders = true
	
    runs {
        client {
            workingDirectory project.file('run')
			
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run')
            args '--mod', '${mod_id}', '--all', '--output', file("src/generated/resources/"), '--existing', file("src/main/resources/")
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")
}

def resourceTargets = ['META-INF/mods.toml']
def replaceProperties = [
        minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
        forge_version: forge_version, forge_version_range: forge_version_range,
        loader_version_range: loader_version_range,
        mod_version: mod_version,
]

processResources {
    inputs.properties replaceProperties
    replaceProperties.put 'project', project

    filesMatching(resourceTargets) {
        expand replaceProperties
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}